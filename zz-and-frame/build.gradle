apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven' // 添加这个

group = 'com.github.zhouzhuo810' // 指定group，com.github.<用户名>

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 23
        versionName "1.22"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    api 'com.android.support:appcompat-v7:27.1.1'
    api 'com.android.support:recyclerview-v7:27.1.1'
    //retrofit
    api 'com.squareup.retrofit2:retrofit:2.4.0'
    //string
    api 'com.squareup.retrofit2:converter-scalars:2.1.0'
    //gson
    api 'com.squareup.retrofit2:converter-gson:2.4.0'
    //rxjava
    api 'io.reactivex:rxjava:1.1.9'
    api 'io.reactivex:rxandroid:1.2.1'
    api 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    //okhttp
    api 'com.squareup.okhttp3:okhttp:3.11.0'
    api 'com.squareup.okhttp3:okhttp-urlconnection:3.10.0'
    api 'com.squareup.okhttp3:logging-interceptor:3.11.0'
    //glide
    api 'com.github.bumptech.glide:glide:3.8.0'
    //andpermission
    api 'com.yanzhenjie:permission:2.0.0-rc12'
    //material design
    api 'com.android.support:design:27.1.1'
}

// 指定编码
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// 打包源码
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// 制作文档(Javadoc)
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
